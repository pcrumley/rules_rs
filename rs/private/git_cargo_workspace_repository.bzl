load("@bazel_tools//tools/build_defs/repo:git_worker.bzl", "git_repo")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "patch")
load(":repository_utils.bzl", "cargo_build_file_values", "inherit_workspace_package_fields")
load(":toml2json.bzl", "run_toml2json")

def _render_label_list(labels):
    return ",\n        ".join(['"%s"' % label for label in sorted(labels)])

def _spoke_repo(hub_name, name, version):
    s = "%s__%s-%s" % (hub_name, name, version)
    if "+" in s:
        s = s.replace("+", "-")
    return s

def _render_build_file(rctx, dest, additive_build_file_content, gen_binaries, workspace_cargo_toml):
    package_path = rctx.path(dest).dirname
    cargo_toml_path = package_path.get_child("Cargo.toml")
    cargo_toml = run_toml2json(rctx, cargo_toml_path)
    cargo_toml = inherit_workspace_package_fields(cargo_toml, workspace_cargo_toml)
    package = cargo_toml["package"]

    cargo = cargo_build_file_values(
        rctx,
        cargo_toml,
        gen_binaries,
        gen_build_script = "on",
        package_path = package_path,
    )

    rctx.file(dest, """\
load("@rules_rs//rs:rust_crate.bzl", "rust_crate")
load("@rules_rs//rs:rust_binary.bzl", "rust_binary")
load("{crate_bzl}", "crate")

crate(
    crate_name = {crate_name},
    crate_root = {crate_root},
    edition = {edition},
    links = {links},
    build_script = {build_script},
    is_proc_macro = {is_proc_macro},
    has_lib = {has_lib},
    binaries = {binaries},
    package_metadata_bazel_deps = [
        {package_metadata_bazel_deps}
    ],
)
{additive_build_file_content}{package_metadata_bazel_additive_build_file_content}""".format(
        crate_bzl = "@%s//:crate.bzl" % _spoke_repo(rctx.attr.hub_name, package["name"], package["version"]),
        crate_name = cargo.values["crate_name"],
        crate_root = cargo.values["crate_root"],
        edition = cargo.values["edition"],
        links = cargo.values["links"],
        build_script = cargo.values["build_script"],
        is_proc_macro = cargo.values["is_proc_macro"],
        has_lib = cargo.values["has_lib"],
        binaries = cargo.values["binaries"],
        package_metadata_bazel_deps = _render_label_list(cargo.bazel_metadata.get("deps", [])),
        additive_build_file_content = additive_build_file_content,
        package_metadata_bazel_additive_build_file_content = cargo.bazel_metadata.get("additive_build_file_content", ""),
    ))

def _unhide_build_file_packages(rctx):
    """Stop the cloned repo's own `.bazelignore` from hiding crate packages we expose.

    The git repo is cloned verbatim, including any `.bazelignore` it ships for its own
    Bazel build. When a requested crate lives under a directory that file ignores — e.g.
    a workspace-excluded vendored subdirectory — the crate BUILD file we generate there
    is unreachable: Bazel reports the package as deleted. Drop any ignore entry that is
    an ancestor of (or equal to) a directory we are about to write a crate BUILD file
    into. Other ignored siblings are left alone, and since only the crates we generate
    are ever referenced, un-ignoring those subtrees has no other effect.
    """
    bazelignore = rctx.path(".bazelignore")
    if not bazelignore.exists:
        return

    package_dirs = [
        dest.rsplit("/", 1)[0] if "/" in dest else ""
        for dest in rctx.attr.build_files.keys()
    ]

    kept = []
    dropped = []
    for line in rctx.read(bazelignore).splitlines():
        entry = line.strip()
        if not entry or entry.startswith("#"):
            kept.append(line)
            continue
        prefix = entry.rstrip("/")
        if any([pkg == prefix or pkg.startswith(prefix + "/") for pkg in package_dirs]):
            dropped.append(entry)
        else:
            kept.append(line)

    if dropped:
        rctx.file(bazelignore, "".join([line + "\n" for line in kept]))
        if rctx.attr.verbose:
            # buildifier: disable=print
            print("rules_rs: dropped .bazelignore entries %s from %s so its crate packages load" % (dropped, rctx.name))

def _git_cargo_workspace_repository_impl(rctx):
    git_repo(rctx, rctx.path("."))

    patch(rctx)
    rctx.delete(rctx.path(".git"))

    _unhide_build_file_packages(rctx)

    workspace_cargo_toml = run_toml2json(rctx, rctx.attr.workspace_cargo_toml)
    for dest, additive_build_file_content in rctx.attr.build_files.items():
        _render_build_file(rctx, dest, additive_build_file_content, rctx.attr.gen_binaries.get(dest, []), workspace_cargo_toml)

    return rctx.repo_metadata(reproducible = True)

git_cargo_workspace_repository = repository_rule(
    implementation = _git_cargo_workspace_repository_impl,
    attrs = {
        "remote": attr.string(mandatory = True),
        "commit": attr.string(mandatory = True),
        "hub_name": attr.string(mandatory = True),
        "shallow_since": attr.string(),
        "init_submodules": attr.bool(default = True),
        "build_files": attr.string_dict(mandatory = True),
        "gen_binaries": attr.string_list_dict(default = {}),
        "workspace_cargo_toml": attr.string(default = "Cargo.toml"),
        "patch_args": attr.string_list(default = []),
        "patches": attr.label_list(default = []),
        "patch_strip": attr.int(default = 0),
        "patch_tool": attr.string(default = ""),
        "recursive_init_submodules": attr.bool(default = True),
        "verbose": attr.bool(default = False),
    },
)
